100 REMark 
110 :
120 MODE 8:CLOSE #2:WINDOW #1,480,256,25,0:intro:skill:wait:init_graph
130 init:setup
140 REPeat move_plane
150 move_right:IF d%=24:win
160 move_left:IF d%=24:win
170 END REPeat move_plane
180 :
190 DEFine PROCedure mainwin
200 PAPER 2,7,1:CLS:BLOCK 385,130,40,78,0:OPEN #3,scr_385x130a60x80
210 PAPER #3,7:CLS #3
220 END DEFine 
230 :
240 DEFine PROCedure topwin
250 BLOCK 200,23,114,28,0:OPEN #4,scr_200x23a135x30
260 CSIZE #4,3,1:INK #4,4:PRINT #4,"  QL BOMBER":CLOSE #4
270 END DEFine 
280 :
290 DEFine PROCedure intro
300 mainwin:topwin:INK #3,0
310 PRINT #3,"  USE YOUR SKILL AND JUDGEMENT"\
320 PRINT #3,"  TO DROP THE BOMBS FROM YOUR"\
330 PRINT #3,"  AIRCRAFT ONTO THE CITY BELOW"\
340 PRINT #3,"  BEFORE YOUR PLANE CRASHES"\
350 PRINT #3,"  INTO ONE OF THE BUILDINGS.YOU"\
360 PRINT #3,"  HAVE AN UNLIMITED SUPPLY OF "\
370 PRINT #3,"  BOMBS.IN THE GAME PRESS THE "\
380 PRINT #3,"  SPACE-BAR TO RELEASE ONE"\:PRINT #3,"  BOMB"\\\
390 INK #3,4:FLASH #3,1:PRINT #3,"  PRESS ANY KEY TO CONTINUE":FLASH #3,0:PAUSE
400 END DEFine 
410 :
420 DEFine PROCedure wait
430 CLS #3:INK #3,1:FLASH #3,1:AT #3,4,5:PRINT #3,"P L E A S E   W A I T":FLASH #3,0:CLOSE #3
440 END DEFine 
450 :
460 DEFine PROCedure init_graph
470 DATA -5245,-10109,-7806,-11646
480 DATA -6783,1665,2,0,8769,8316
490 DATA 0,0,9264,18432,8834,-11524,128
500 DATA 22664,-20228,32,26350,28672,20085
510 DO_GRAPH=RESPR(100):RESTORE 470
520 FOR I=DO_GRAPH TO DO_GRAPH+22*2 STEP 2:READ B:POKE_W I,B
530 C_TAB=RESPR (32*8)
540 DEF_GRAPH 0,2,1,255,153,255,153,255,153,255,255
550 DEF_GRAPH 1,7,0,0,0,0,128,222,255,0,0
560 DEF_GRAPH 2,7,0,0,0,0,1,123,255,0,0
570 DEF_GRAPH 3,0,0,0,0,0,0,0,0,0,0
580 DEF_GRAPH 4,1,0,36,24,60,60,60,60,24,24
590 DEF_GRAPH 5,6,0,145,90,60,254,127,60,90,145
600 DEF_GRAPH 6,2,0,0,16,121,102,252,94,59,254
610 DEF_GRAPH 7,4,0,0,0,0,128,195,235,239,255
620 END DEFine 
630 :
640 DEFine PROCedure DEF_GRAPH(S%,F%,B%,R0%,R1%,R2%,R3%,R4%,R5%,R6%,R7% )
650 LOCal A,FH%,FL%,BH%,BL%:A=C_TAB+32*S%
660 FH%=(F% DIV 4)*2:FL%=F%&&3
670 BH%=(B% DIV 4)*2:BL%=B%&&3
680 charline(R0%):charline(R1%):charline (R2%):charline (R3%)
690 charline(R4%):charline(R5%):charline (R6%):charline (R7%)
700 END DEFine 
710 :
720 DEFine PROCedure charline(r%)
730 LOCal m%,j,I,dl%,dh%:m%=128
740 FOR j=1 TO 2
750 dl%=0:dh%=0
760 FOR I=1 TO 4
770 IF (r%&&m%)=m% THEN dl%=FH%+dl%*4:dh%=FL%+dh%*4:ELSE dl%=BH%+dl%:dh%=BL%+dh%*4
780 m%=m% DIV 2
790 END FOR I
800 POKE A,dl%:POKE A+1,dh%:A=A+2
810 END FOR j
820 END DEFine 
830 :
840 DEFine PROCedure graph(x%,y%,S%)
850 CALL DO_GRAPH,x%,y%,S%,C_TAB
860 END DEFine 
870 :
880 DEFine PROCedure skill
890 CLS #3:INK #3,1:PRINT #3\\"  [PRESS 'T' FOR TRAINEE]"\
900 PRINT #3,"  [PRESS 'S' FOR STANDARD]"\
910 PRINT #3,"  [PRESS 'E' FOR EXPERT]"\\
920 INK #3,0:PRINT #3,"PRESS THE KEY FOR THE SKILL"\
930 PRINT #3,"LEVEL YOU WANT."
940 REPeat KEYS
950 POKE 163976,1:S%=CODE(INKEY$(-1))
960 IF S%=69:DIFF=7:EXIT KEYS
970 IF S%=83:DIFF=12:EXIT KEYS
980 IF S%=84:DIFF=17:EXIT KEYS
990 END REPeat KEYS
1000 END DEFine 
1010 :
1020 DEFine PROCedure setup
1030 PAPER 0:CLS:BLOCK 110,10,168,16,5:OPEN #5,SCR_110X10A195X18
1040 PAPER #5,6:CLS #5:INK #5,0:PRINT #5,"QL BOMBER":CLOSE #5
1050 FOR A=5 TO 26
1060 FOR B=RND(DIFF TO 25) TO 24
1070 graph A,B,0
1080 END FOR B
1090 END FOR A
1100 graph C%,d%,1:INK 4,5,1
1110 FOR Z=15 TO 21 STEP .2:LINE 0,Z TO 200,Z
1120 END DEFine 
1130 :
1140 DEFine PROCedure init
1150 RANDOMISE:C%=3:d%=4:x%=200:y%=200:F%=0:CAT%=400:SCORE%=0
1160 END DEFine 
1170 :
1180 DEFine PROCedure move_right
1190 graph C%,d%,3:d%=d%+1:graph C%,d%,1:x%=x%-6:y%=y%-6
1200 BEEP 0,x%,y%,10,10,14,1,2
1210 REPeat RIGHT
1220 graph C%,d%,3:C%=C%+1:graph C%,d%,1
1230 IF PIX(d%,C%+1,4,3)<>0:CRASH_R
1240 IF C%=28:EXIT RIGHT
1250 IF F%:BOMB
1260 IF F%=0 AND KEYROW(1)=64:F%=1:G%=C%:H%=d%+1:BOMB
1270 END REPeat RIGHT
1280 END DEFine 
1290 :
1300 DEFine PROCedure move_left
1310 graph C%,d%,3:d%=d%+1:graph C%,d%,2:x%=x%-6:y%=y%-6
1320 BEEP 0,x%,y%,10,10,14,1,2
1330 REPeat LEFT
1340 graph C%,d%,3:C%=C%-1:graph C%,d%,2
1350 IF PIX(d%,C%-1,4,3)<>0:CRASH_L
1360 IF C%=3:EXIT LEFT
1370 IF F%:BOMB
1380 IF F%=0 AND KEYROW(1)=64:F%=1:G%=C%:H%=d%+1:BOMB
1390 END REPeat LEFT
1400 END DEFine 
1410 :
1420 DEFine PROCedure BOMB
1430 graph G%,H%,3:H%=H%+2:graph G%,H%,4
1440 IF H%>24:TROUGH1:RETurn 
1450 IF H%>23:TROUGH2:RETurn 
1460 IF PIX(H%+1,G%,4,3)<>0:EXPL
1470 BEEP 0,x%,y%,10,10,15,1,2
1480 END DEFine 
1490 :
1500 DEFine PROCedure TROUGH1
1510 F%=0:REMark graph G%,H%-1,3:graph G%,H%,3:graph G%,H%-1,7
1520 END DEFine 
1530 :
1540 DEFine PROCedure TROUGH2
1550 F%=0:graph G%,H%,3:graph G%,H%-1,3:graph G%,H%+1,7
1560 END DEFine 
1570 :
1580 DEFine PROCedure EXPL
1590 BEEP 0,3,6,4,4,15,10,5
1600 REMark graph G%,H%-1,3:graph G%,H%,3:graph G%,H%+1.5:graph G%,H%+1,3:F%=0:SCORE%=SCORE%+5
1605 SCORE%=SCORE%+5
1610 END DEFine 
1620 :
1630 DEFine PROCedure win
1640 BEEP:J%=1:graph 3,23,3:graph 3,24,1
1650 REPeat SOUND:BEEP 0,J%:J%=J%+1:IF J%=40:EXIT SOUND
1660 BEEP:STRIP 2:INK 6:AT 5,10:PRINT "CONGRATULATIONS!":CATCH:GET_SCORE:GAME_AGAIN
1670 END DEFine win
1680 :
1690 DEFine PROCedure CRASH_R
1700 graph G%,H%,3:graph C%,d%,3
1710 graph C%+1,d%,5:x%=0:y%=0
1720 REPeat SOUNDR:x%=x%+1:y%=y%+1:BEEP 0,20,40,4,4,15,x%,y%:IF x%=15:EXIT SOUNDR
1730 BEEP:graph C%+1,d%,6:CATCH:GET_SCORE:GAME_AGAIN
1740 END DEFine 
1750 :
1760 DEFine PROCedure CRASH_L
1770 graph G%,H%,3:graph C%,d%,3
1780 graph C%-1,d%,5:x%=0:y%=0
1790 REPeat SOUNDL:x%=x%+1:y%=y%+1:BEEP 0,20,40,4,4,15,x%,y%:IF x%=15:EXIT SOUNDL
1800 BEEP:graph C%+1,d%,6:CATCH:GET_SCORE:GAME_AGAIN
1810 END DEFine 
1820 :
1830 DEFine PROCedure GAME_AGAIN
1840 CATCH:mainwin:topwin:INK #3,1
1850 PRINT #3\\"  [PRESS 'Y' FOR YES]"\
1860 PRINT #3,"  [PRESS 'N' FOR NO]"\\
1870 INK #3,0:PRINT #3,"  DO YOU WANT ANOTHER GAME?"
1880 REPeat SCAN
1890 POKE 163976,1:A%=CODE(INKEY$(-1))
1900 IF A%=78:CLOSE #3:OPEN #2,SCR:PAPER 0:PAPER #2,1:INK #2,7:STOP
1910 IF A%=89:EXIT SCAN
1920 END REPeat SCAN
1930 mainwin:topwin:skill:GO TO 130
1940 END DEFine 
1950 :
1960 DEFine PROCedure CATCH
1965 CAT%=700
1970 REPeat DELAY:CAT%=CAT%-1:IF CAT%=0:EXIT DELAY
1980 END DEFine 
1990 :
2000 DEFine PROCedure GET_SCORE
2010 STRIP 2:INK 6:FLASH 1:AT 7,13:PRINT "SCORE=";SCORE%:FLASH 0:CATCH
2020 END DEFine 
2030 :
2040 DEFine FuNction PIX(r%,C%,x%,y%)
2050 LOCal A%,P%,B%,A:P%=C%*8+x%
2060 A=131072+(512*r%+y%*64+P% DIV 4)*2
2070 B%=PEEK(A+1):A%=PEEK(A):P%=P% MOD 4
2080 IF P%=3:RETurn ((2 && A%)*2)+(3&&B%)
2090 IF P%=2:RETurn ((8 &&A%)DIV 2)+((12 &&B%)DIV 4)
2100 IF P%=1:RETurn ((32&&A%)DIV 8)+((48&&B%)DIV 16)
2110 IF P%=0:RETurn ((128&&A%)DIV 32)+((192&&B%)DIV 64)
2120 END DEFine 
